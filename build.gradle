plugins {
    id 'java'
    id "io.qameta.allure" version "3.0.2"
    id 'org.gradle.test-retry' version '1.6.4'
}

repositories {
    mavenCentral()
}

def allureVersion = '2.32.0',
    selenideVersion = '7.14.0',
    junitVersion = '6.0.3',
    junitPEVersion = '6.0.3',
    slf4jVersion = '2.0.17',
    assertjCoreVersion = '3.27.7',
    aspectjweaverVersion = '1.9.25.1',
    ownerVersion = '1.0.12'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileTestJava {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation(
            "org.assertj:assertj-core:$assertjCoreVersion",
            "org.aspectj:aspectjweaver:$aspectjweaverVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "com.codeborne:selenide:$selenideVersion",
            "org.junit.platform:junit-platform-engine:$junitPEVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testRuntimeOnly(
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.junit.platform:junit-platform-launcher",
            "org.junit.jupiter:junit-jupiter-engine:$junitVersion")
}

test {
    systemProperties += System.properties
    useJUnitPlatform{
        if (System.getProperty('excludeTags')) {
            excludeTags(System.getProperty('excludeTags').split(','))
        }}
    retry {
        failOnPassedAfterRetry = false
        maxFailures = 12
        maxRetries = 2
    }
}